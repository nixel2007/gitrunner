// Реализация шагов BDD-фич/сценариев c помощью фреймворка https://github.com/artbear/1bdd

#Использовать asserts
#Использовать tempfiles

#Использовать "../.."

Перем БДД; //контекст фреймворка 1bdd

// Метод выдает список шагов, реализованных в данном файле-шагов
Функция ПолучитьСписокШагов(КонтекстФреймворкаBDD) Экспорт
	БДД = КонтекстФреймворкаBDD;

	ВсеШаги = Новый Массив;

	ВсеШаги.Добавить("ЯСоздаюНовыйОбъектГитрепозиторий");
	ВсеШаги.Добавить("ЯУстанавливаюРабочийКаталог");
	ВсеШаги.Добавить("РабочийКаталогУстановленВ");
	ВсеШаги.Добавить("ЯПытаюсьУстановитьРабочийКаталог");
	ВсеШаги.Добавить("ВыбрасываетсяИсключениеСТекстом");
	ВсеШаги.Добавить("ЯУстанавливаюПутьКGit");
	ВсеШаги.Добавить("ПутьКGitУстановленВ");

	Возврат ВсеШаги;
КонецФункции

// Реализация шагов

// Процедура выполняется перед запуском каждого сценария
Процедура ПередЗапускомСценария(Знач Узел) Экспорт
	
КонецПроцедуры

// Процедура выполняется после завершения каждого сценария
Процедура ПослеЗапускаСценария(Знач Узел) Экспорт

КонецПроцедуры

//Я устанавливаю рабочий каталог "."
Процедура ЯУстанавливаюРабочийКаталог(Знач ПутьРабочийКаталог) Экспорт
	ГитРепозиторий = БДД.ПолучитьИзКонтекста("ГитРепозиторий");
	ГитРепозиторий.УстановитьРабочийКаталог(ПутьРабочийКаталог);
КонецПроцедуры

//Рабочий каталог установлен в "."
Процедура РабочийКаталогУстановленВ(Знач ПутьРабочийКаталог) Экспорт
	Файл = Новый Файл(ПутьРабочийКаталог);
	ГитРепозиторий = БДД.ПолучитьИзКонтекста("ГитРепозиторий");
	Ожидаем.Что(Файл.ПолноеИмя).Равно(ГитРепозиторий.ПолучитьРабочийКаталог());
КонецПроцедуры

//Я создаю новый объект ГитРепозиторий
Процедура ЯСоздаюНовыйОбъектГитрепозиторий() Экспорт
	ГитРепозиторий = Новый ГитРепозиторий;
	БДД.СохранитьВКонтекст("ГитРепозиторий", ГитРепозиторий);
КонецПроцедуры

//я устанавливаю путь к git "fake/bin/git"
Процедура ЯУстанавливаюПутьКGit(Знач ПутьКГит) Экспорт
	ГитРепозиторий = БДД.ПолучитьИзКонтекста("ГитРепозиторий");
	ГитРепозиторий.УстановитьПутьКГит(ПутьКГит);
КонецПроцедуры

//Путь к git установлен в "fake/bin/git"
Процедура ПутьКGitУстановленВ(Знач ПутьКГит) Экспорт
	ГитРепозиторий = БДД.ПолучитьИзКонтекста("ГитРепозиторий");
	Ожидаем.Что(ГитРепозиторий.ПолучитьПутьКГит()).Равно(ПутьКГит);
КонецПроцедуры

//Я пытаюсь установить рабочий каталог "/fake/workspace"
Процедура ЯПытаюсьУстановитьРабочийКаталог(Знач ПутьРабочийКаталог) Экспорт
	
	ГитРепозиторий = БДД.ПолучитьИзКонтекста("ГитРепозиторий");
	БДД.СохранитьВКонтекст("ТекстИсключения", "");
	Попытка
		ГитРепозиторий.УстановитьРабочийКаталог(ПутьРабочийКаталог);
	Исключение
		БДД.СохранитьВКонтекст("ТекстИсключения", ОписаниеОшибки());
	КонецПопытки;

КонецПроцедуры

//Выбрасывается исключение с текстом "не существует"
Процедура ВыбрасываетсяИсключениеСТекстом(Знач ИскомыйТекстИсключения) Экспорт
	ТекстИсключения = БДД.ПолучитьИзКонтекста("ТекстИсключения");
	Ожидаем.Что(ТекстИсключения).Содержит(ИскомыйТекстИсключения);
КонецПроцедуры




